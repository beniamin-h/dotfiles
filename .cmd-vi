vim_ins_mode="%{$fg[cyan]%}I%{$reset_color%}"
vim_cmd_mode="%{$fg[red]%}C%{$reset_color%}"
vim_mode=$vim_ins_mode

function zle-keymap-select {
  vim_mode="${${KEYMAP/vicmd/${vim_cmd_mode}}/(main|viins)/${vim_ins_mode}}"
  set-prompt
  zle reset-prompt
}
zle -N zle-keymap-select

function zle-line-finish {
  vim_mode=$vim_ins_mode
  set-prompt
}
zle -N zle-line-finish

function TRAPINT() {
  vim_mode=$vim_ins_mode
  set-prompt
  return $(( 128 + $1 ))
} 

set -o vi

function set-prompt {
  PROMPT="%{$fg_bold[green]%}╭─%{$reset_color%}${current_dir} ${git_branch}%{$fg[magenta]%}%D{%I:%M:%S}%{$reset_color%}
%{$fg_bold[green]%}╰─%{$reset_color%}%B${vim_mode}%b "
}

set-prompt

bindkey -M vicmd 'k' up-line-or-beginning-search
bindkey -M vicmd 'j' down-line-or-beginning-search

